LIBNAME TITANLIB "Z:\TANUMOY\WORKSPACE\DATA\TITAN\REFINED MODEL";
OPTIONS MACROGEN SYMBOLGEN MLOGIC MPRINT;

/* VARIABLE UNIVERSE 
DEPENDENTS= RTN_GRP (LOGISTIC REGRESSION)
            AVG_NET_RTN_TXN (LINEAR REGRESSION)
INDEPEDENTS=AVG_SHP_UNT_TXN NUM_TXNS DAYS_&BRD._CUST DAYS_GID_CUST
			AVG_NET_DMD_TXN AVG_TOT_REV_TXN AVG_VAR_MGN_TXN
            AVG_NET_DMD_UNT AVG_TOT_REV_UNT AVG_VAR_MGN_UNT DISCOUNT_PCT
			ACTIVE_FLAG NEW_FLAG DIV_PURCHASED AVG_DISCOUNT_TXN
*/


%MACRO MODEL(BRD, COLLIN, STEPWISE, DSNAME, TRAIN_PCT, SEED, VARLIST, PARTITIONDS);
 %IF &PARTITIONDS EQ 1 %THEN %DO;
   DATA TEMP1;
    SET &DSNAME;
	WHERE PRD_BRD_CD="&BRD";
	RANDOM=RANUNI(&SEED);
	DATATYPE="TRAIN";
	IF RANDOM>&TRAIN_PCT/100 THEN DATATYPE="TEST";
   RUN;

   DATA TITANLIB.&BRD._TRAIN TITANLIB.&BRD._TEST;
    SET TEMP1;
	IF DATATYPE="TRAIN" THEN OUTPUT TITANLIB.&BRD._TRAIN;
    IF DATATYPE="TEST" THEN OUTPUT TITANLIB.&BRD._TEST;
   RUN;
 %END;
  
 %IF &COLLIN EQ 1 AND &STEPWISE EQ 0 %THEN %DO; 
  PROC REG DATA=TITANLIB.&BRD._TRAIN;
 	MODEL AVG_NET_RTN_TXN =    &VARLIST/VIF COLLIN TOL;
  QUIT; 
 %END;

 %IF &STEPWISE EQ 0 AND &COLLIN EQ 0 %THEN %DO;
   PROC LOGISTIC DATA=TITANLIB.&BRD._TRAIN;
	 MODEL RTN_GRP(EVENT='1')= &VARLIST/CTABLE LACKFIT RSQ;
   QUIT;
 %END;

 %IF &STEPWISE=1 AND &COLLIN EQ 0 %THEN %DO;
	PROC LOGISTIC DATA=TITANLIB.&BRD._TRAIN OUTEST=TITANLIB.&BRD._EST;
	 MODEL RTN_GRP(EVENT='1')= &VARLIST/CTABLE LACKFIT RSQ SELECTION=STEPWISE;
	QUIT;
	
	PROC TRANSPOSE DATA=TITANLIB.&BRD._EST OUT=TEST;
	RUN;
	
	DATA TEST;
	 SET TEST;
	 RENAME _NAME_=VARIABLE;
	 DROP _LABEL_;
	 RENAME RTN_GRP=ESTIMATES;
	RUN;

	DATA TEST;
	 SET TEST;
	 LABEL VARIABLE='VARIABLE NAME';
	 IF VARIABLE="Intercept" THEN VARIABLE="INTERCEPT";
	 IF VARIABLE  NE "_LNLIKE_" AND ESTIMATES=. THEN DELETE;
	 IF VARIABLE  NE "_LNLIKE_" THEN LIKELIHOOD=EXP(ESTIMATES);
	RUN;

	PROC SQL NOPRINT;
	 SELECT DISTINCT VARIABLE INTO : VARLIST SEPARATED BY ' ' FROM TEST
            WHERE VARIABLE NE "INTERCEPT" AND VARIABLE NE "_LNLIKE_";
    QUIT;

   
	PROC LOGISTIC DATA=TITANLIB.&BRD._TRAIN OUTEST=TITANLIB.&BRD._EST OUTMODEL=TITANLIB.&BRD._MODEL;
	 MODEL RTN_GRP(EVENT='1')= &VARLIST/
	                           CTABLE RSQ EXPB OUTROC=TITANLIB.&BRD._ROC;
	 OUTPUT OUT=TITANLIB.&BRD._PRED P=PHAT LOWER=LCL UPPER=UCL PREDPROBS=(INDIVIDUAL CROSSVALIDATE) XBETA=LOGIT;
	QUIT;


	PROC GPLOT DATA=TITANLIB.&BRD._ROC;
	 PLOT _SENSIT_*_1MSPEC_=1 / VAXIS=0 TO 1 BY .1;
	 SYMBOL COLOR=BLUE INTERPOL=JOIN VALUE=DOT HEIGHT=0.1;
	QUIT;
    
	PROC LOGISTIC INMODEL=TITANLIB.&BRD._MODEL;
	 SCORE DATA=TITANLIB.&BRD._TEST OUT=TITANLIB.&BRD._TEST;
	RUN;
    
	PROC FREQ DATA=TITANLIB.&BRD._TEST;
     TABLES F_RTN_GRP*I_RTN_GRP/NOROW NOCOL;
    RUN;

    %LET LIB=Z:\TANUMOY\WORKSPACE\DATA\TITAN\REFINED MODEL;

	PROC EXPORT DATA=TITANLIB.&BRD._ROC
       OUTFILE="&LIB.\&BRD._ROC.TXT" DBMS=DLM REPLACE;
	   DELIMITER='|';
	RUN;
    
	PROC EXPORT DATA=TITANLIB.&BRD._PRED
       OUTFILE="&LIB.\&BRD._PRED.TXT" DBMS=DLM REPLACE;
	   DELIMITER='|';
	RUN;
     
    PROC EXPORT DATA=TITANLIB.&BRD._TEST;
       OUTFILE="&LIB.\&BRD._TEST.TXT" DBMS=DLM REPLACE;
	   DELIMITER='|';
	RUN;

	PROC EXPORT DATA=TITANLIB.&BRD._TRAIN
       OUTFILE="&LIB.\&BRD._TRAIN.TXT" DBMS=DLM REPLACE;
	   DELIMITER='|';
	RUN;

	PROC TRANSPOSE DATA=TITANLIB.&BRD._EST OUT=TEST;
	RUN;
	
	DATA TEST;
	 SET TEST;
	 RENAME _NAME_=VARIABLE;
	 DROP _LABEL_;
	 RENAME RTN_GRP=ESTIMATES;
	RUN;

	DATA TEST;
	 SET TEST;
	 LABEL VARIABLE = "VARIABLE NAME";
	 IF VARIABLE="Intercept" THEN VARIABLE="INTERCEPT";
	 IF VARIABLE  NE "_LNLIKE_" AND ESTIMATES=. THEN DELETE;
	 IF VARIABLE  NE "_LNLIKE_" THEN LIKELIHOOD=EXP(ESTIMATES);
	RUN;

	PROC EXPORT DATA=TEST
       OUTFILE="&LIB.\&BRD._EST.TXT" DBMS=DLM REPLACE;
	   DELIMITER='|';
	RUN;

  %END;

%MEND;


%MODEL(BRD=GOL, COLLIN=1, STEPWISE=0, TRAIN_PCT=80, SEED=12345, DSNAME=TITANLIB.DECILES_BRD_GRP_MODEL, PARTITIONDS=1,
       VARLIST=AVG_SHP_UNT_TXN NUM_TXNS DAYS_GOL_CUST DAYS_GID_CUST AVG_NET_DMD_TXN AVG_TOT_REV_TXN AVG_VAR_MGN_TXN
       AVG_NET_DMD_UNT AVG_TOT_REV_UNT AVG_VAR_MGN_UNT DISCOUNT_PCT ACTIVE_FLAG NEW_FLAG DIV_PURCHASED AVG_DISCOUNT_TXN);

%MODEL(BRD=GOL, COLLIN=0, STEPWISE=1, TRAIN_PCT=80, SEED=12345, DSNAME=TITANLIB.DECILES_BRD_GRP_MODEL, PARTITIONDS=0,
       VARLIST=AVG_SHP_UNT_TXN NUM_TXNS DAYS_GOL_CUST AVG_NET_DMD_TXN AVG_VAR_MGN_TXN
       AVG_NET_DMD_UNT AVG_VAR_MGN_UNT DISCOUNT_PCT ACTIVE_FLAG NEW_FLAG DIV_PURCHASED AVG_DISCOUNT_TXN);

DM "LOG" CLEAR;
DM "OUTPUT" CLEAR;
